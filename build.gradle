plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.freefair.aggregate-javadoc" version "6.5.0.3"
    id "io.freefair.aggregate-javadoc-jar" version "6.6.3"
    id 'maven-publish'
}

group 'me.epic'
version '1.2.8-SNAPSHOT'
description "Epic's Spigot Lib"

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

jar.enabled = false

subprojects {

    java {
        withSourcesJar()
        withJavadocJar()
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }


    def javadocConfig = {
        options {
            links(
                    'https://docs.oracle.com/en/java/javase/16/docs/api/',
                    'https://hub.spigotmc.org/javadocs/spigot/',
                    'https://javadoc.io/doc/org.jetbrains/annotations/19.0.0',
                    'https://repo.jeff-media.com/javadoc/public/com/jeff_media/MorePersistentDataTypes/2.4.0/raw/'
            )
            tags(
                    "nms:a:Requires NMS"
            )
            addStringOption('Xdoclint:none', '-quiet')
        }
    }


    javadoc { configure javadocConfig }
    aggregateJavadoc { configure javadocConfig }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
        }
        maven {
            url = "https://repo.papermc.io/repository/maven-public/"
        }
    }
}

dependencies {
    implementation project(':epicspigotlib-core')
    implementation project(path: ':spigot-1_17_R1', configuration: 'reobf')
    implementation project(path: ':spigot-1_18_R1', configuration: 'reobf')
    implementation project(path: ':spigot-1_18_R2', configuration: 'reobf')
    implementation project(path: ':spigot-1_19_R1', configuration: 'reobf')
    implementation project(path: ':spigot-1_19_R2', configuration: 'reobf')
    implementation project(path: ':spigot-1_19_R3', configuration: 'reobf')
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.jeff_media.morepersistentdatatypes', 'me.epic.morepersistentdatatypes'
    relocate 'net.kyori', 'me.epic.kyori'
}

tasks.register('combineSourcesJar', Jar) {
    archiveClassifier.set("sources")

    // Collect all source directories from submodules
    def packages = subprojects.collect { project ->
        project.file("src/main/java")
    }

    // Include the source files in the combined sources JAR
    from(packages) {
        include '**/*.java'
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact(aggregateJavadocJar)
            publication.artifact(combineSourcesJar)
        }
    }
    repositories {
        maven {
            credentials {
                username = "epic"
                password = System.getenv("EPICREPO_SECRET")
            }
            url = 'https://repo.epicebic.xyz/public/'
        }
    }
}